plugins {
	id 'com.github.johnrengelman.shadow' version '5.0.0'
	id 'java'
}


defaultTasks 'build'


group 'org.hurricanegames'
version '1.3'


import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.Files


Path projectDirectoryPath = projectDir.toPath().toAbsolutePath()


sourceCompatibility = JavaVersion.VERSION_1_8

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
		resources {
			srcDirs = ['resources']
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/public/'
	}
	maven {
		url 'https://papermc.io/repo/repository/maven-public/'
	}
	maven {
		url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
	}
	maven {
		url 'https://jitpack.io'
	}
}

configurations {
	runtimeDependencies
	compileOnly {
		extendsFrom runtimeDependencies
	}
	testImplementation {
	    extendsFrom runtimeDependencies
	}
}

dependencies {
	runtimeDependencies group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.5-R0.1-SNAPSHOT'

	implementation group: 'com.github.HurricaneGamesOrg', name: 'HurricaneGamesOrgPluginLib', version: '997fc2eb6b'
}

compileJava {
	options.encoding = 'UTF-8'
	options.incremental = false
}


processResources {
	filesMatching('plugin.yml') {
		expand 'version': project.version
	}
}

shadowJar {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	archiveClassifier = ""

	from sourceSets.main.java.srcDirs
	from 'LICENSE'

	String relocatePrefix = project.group + '.autokeyboardlayout.zlibs.'

	relocate 'org.hurricanegames.pluginlib', relocatePrefix + 'pluginlib'

	minimize()
}


task copyFinalJarToTarget(type: DefaultTask) {doLast{
	Path targetJarDirectory = projectDirectoryPath.resolve('target')
	Files.createDirectories(targetJarDirectory)
	Files.copy(
		shadowJar.archiveFile.get().getAsFile().toPath().toAbsolutePath(),
		targetJarDirectory.resolve(shadowJar.archiveBaseName.get() + '.jar'),
		java.nio.file.StandardCopyOption.REPLACE_EXISTING
	)
}}


compileJava.dependsOn(clean)
jar.enabled = false
jar.finalizedBy(shadowJar)
shadowJar.finalizedBy(copyFinalJarToTarget)
